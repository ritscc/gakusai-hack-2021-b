/* =====================
  build setting
===================== */
buildscript {
    ext {
        springBootVersion = "2.5.6"
    }
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
        classpath "org.flywaydb:flyway-gradle-plugin:7.1.1"
        classpath "org.yaml:snakeyaml:1.28"
    }
}

apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "org.flywaydb.flyway"
apply plugin: "java"
apply plugin: "groovy"
apply plugin: "eclipse"
apply plugin: "idea"

java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

repositories {
    mavenCentral()
}

configurations {
    mybatisGenerate
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-sleuth:3.0.4"
    }
}

/* =====================
  variables
===================== */
ext {
    // path
    PROJECT_ROOT_PATH = projectDir.absolutePath.replaceAll("\\\\", "/")
    JAVA_ROOT_PATH = "${PROJECT_ROOT_PATH}/src/main/java"
    RESOURCES_ROOT_PATH = "app/src/main/resources"
    PROPERTY_ROOT_PATH = "${rootProject.rootDir.path}/${RESOURCES_ROOT_PATH}"
    GRADLE_ROOT_PATH = "../gradle"

    // application property file
    BASE_PROPERTY_FILE = "application.yml"
    ENVIRONMENT =  System.getProperty("spring.profiles.active") ?: "local"
    EXTENSION_PROPERTY_FILE = "application-${ENVIRONMENT}.yml"

    // application property
    BASE_PROPERTY = new org.yaml.snakeyaml.Yaml().load(new File("${PROPERTY_ROOT_PATH}/${BASE_PROPERTY_FILE}").newInputStream())
    EXTENSION_PROPERTY = new org.yaml.snakeyaml.Yaml().load(new File("${PROPERTY_ROOT_PATH}/${EXTENSION_PROPERTY_FILE}").newInputStream())

    // application info
    APPLICATION_NAME = BASE_PROPERTY.project.name
    APPLICATION_VERSION = BASE_PROPERTY.project.version

    // datasource
    DATASOURCE_URL = EXTENSION_PROPERTY.spring.datasource.url
    DATASOURCE_USERNAME = EXTENSION_PROPERTY.spring.datasource.username
    DATASOURCE_PASSWORD = EXTENSION_PROPERTY.spring.datasource.password
    DATASOURCE_DRIVER = EXTENSION_PROPERTY.spring.datasource.driver

    // mybatis
    MYBATIS_PROPERTY = BASE_PROPERTY.project.mybatis
}


/* =====================
  dependencies
===================== */
dependencies {
    // spring boot starter
    implementation "org.springframework.boot:spring-boot-starter-web"
    implementation "org.springframework.boot:spring-boot-starter-websocket"
    implementation "org.springframework.boot:spring-boot-starter-validation"

    // spring security
    implementation "org.springframework.boot:spring-boot-starter-security"
    implementation "org.springframework.security:spring-security-core"
    implementation "org.springframework.security:spring-security-web"
    implementation "org.springframework.security:spring-security-config"

    // spring cloud
    implementation "org.springframework.cloud:spring-cloud-starter-sleuth"

    // JWT
    implementation "io.jsonwebtoken:jjwt:0.9.1"

    // flyway
    implementation "org.flywaydb:flyway-core:7.1.1"

    // lombok
    implementation "org.projectlombok:lombok:1.18.20"
    annotationProcessor "org.projectlombok:lombok:1.18.20"
    testImplementation "org.projectlombok:lombok:1.18.20"
    testAnnotationProcessor "org.projectlombok:lombok:1.18.20"

    // mybatis
    implementation "org.mybatis.spring.boot:mybatis-spring-boot-starter:2.2.0"
    implementation "org.mybatis.generator:mybatis-generator-maven-plugin:1.4.0"
    mybatisGenerate "org.mybatis.generator:mybatis-generator-core:1.4.0"
    mybatisGenerate "com.softwareloop:mybatis-generator-lombok-plugin:1.0"
    mybatisGenerate "mysql:mysql-connector-java"

    // mysql
    implementation "mysql:mysql-connector-java"

    // swagger
    implementation "io.springfox:springfox-boot-starter:3.0.0"
    implementation "io.springfox:springfox-swagger2:3.0.0"
    implementation "io.springfox:springfox-swagger-ui:3.0.0"

    // test
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.jmockit:jmockit:1.49"
    testImplementation "org.jeasy:easy-random-core:5.0.0"

    // logging
    implementation "ch.qos.logback:logback-classic:1.2.3"

    // file util
    implementation "org.yaml:snakeyaml:1.28"
    implementation "net.rakugakibox.util:yaml-resource-bundle:1.2"
    implementation "com.google.code.gson:gson:2.8.7"

    // util
    implementation "org.modelmapper:modelmapper:2.4.4"
    implementation "org.apache.commons:commons-collections4:4.4"
    implementation "commons-net:commons-net:3.6"
}

/* =====================
  gradle
===================== */
apply from: "${GRADLE_ROOT_PATH}/jar.gradle"
apply from: "${GRADLE_ROOT_PATH}/run.gradle"
apply from: "${GRADLE_ROOT_PATH}/test.gradle"
apply from: "${GRADLE_ROOT_PATH}/flyway.gradle"
apply from: "${GRADLE_ROOT_PATH}/javadoc.gradle"
apply from: "${GRADLE_ROOT_PATH}/mybatis.gradle"
